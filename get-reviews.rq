prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix dct: <http://purl.org/dc/terms/>
prefix np: <http://www.nanopub.org/nschema#>
prefix npx: <http://purl.org/nanopub/x/>
prefix npa: <http://purl.org/nanopub/admin/>
prefix frbr: <http://purl.org/vocab/frbr/core#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

prefix lf: <https://w3id.org/linkflows/reviews/>

select distinct ?node1 ?rel ?node2 where {

  graph ?submission_np_head {
    ?submission_np np:hasAssertion ?submission_np_assertion ;
      np:hasPublicationInfo ?submission_np_pubinfo ;
      a np:Nanopublication .
  }
  graph ?submission_np_assertion {
    ?submitted_np frbr:partOf <https://w3id.org/linkflows/formalization-papers/DataScienceSpecialIssue> .
    bind(replace(str(?submitted_np), '^.*(RA[a-zA-Z0-9-_]{8})[a-zA-Z0-9-_]{35}$', '$1') as ?submitted_npLabel)
  }
  graph ?submission_np_pubinfo {
    ?submission_np dct:creator ?__author_iri .
    bind(?__author_iri as ?author)
  }

  filter not exists {
    graph npa:graph { ?newversion npa:hasValidSignatureForPublicKey ?pubkey . } ?newversion np:hasPublicationInfo ?si .  # TODO: move into npa:graph
    graph ?si { ?newversion npx:supersedes ?submission_np . }
  }
  filter not exists {
    graph npa:graph { ?retraction_s npa:hasValidSignatureForPublicKey ?pubkey . } ?retraction_s np:hasAssertion ?sa .  # TODO: move into npa:graph
    graph ?sa { ?somebody_s npx:retracts ?submission_np . }
  }

  graph ?review_np_head {
    ?review_np np:hasAssertion ?review_np_assertion ;
      np:hasPublicationInfo ?review_np_pubinfo ;
      a np:Nanopublication .
    bind(replace(str(?review_np), '^.*(RA[a-zA-Z0-9-_]{8})[a-zA-Z0-9-_]{35}$', '$1') as ?review_npLabel)
  }
  graph ?review_np_assertion {
    ?r a lf:ReviewComment .
    ?r lf:refersTo ?submitted_np .
  }
  graph ?review_np_pubinfo {
    ?review_np dct:creator ?reviewer_iri .
    bind(?reviewer_iri as ?reviewer)
  }

  filter not exists {
    graph npa:graph { ?newversion npa:hasValidSignatureForPublicKey ?reviewer_pubkey . } ?newversion np:hasPublicationInfo ?ri .  # TODO: move into npa:graph
    graph ?ri { ?newversion npx:supersedes ?review_np . }
  }
  filter not exists {
    graph npa:graph { ?retraction_rv npa:hasValidSignatureForPublicKey ?reviewer_pubkey . } ?retraction_rv np:hasAssertion ?ra .  # TODO: move into npa:graph
    graph ?ra { ?somebody_rv npx:retracts ?review_np . }
  }

  bind(?review_npLabel as ?node1)
  bind(concat("[fillcolor=blue,URL=", ?review_np, "]") as ?rel)
  bind("" as ?node2)

}
